_commit=ecd02c178af5efbac38d427a8d178f940327de1f

pkgbase=sing-geoip
pkgname=(sing-geoip-db sing-geoip-rule-set)
pkgver=20260207045628
pkgrel=1
arch=(any)
url=https://github.com/SagerNet/sing-geoip
license=(GPL-3.0-or-later)
makedepends=(go)
source=("$pkgbase-$_commit.tar.gz::$url/archive/$_commit.tar.gz")
sha256sums=('4da5600e19c872f539853261c84936b9e08344cadf85561cf599ac3f8a58925b')

prepare() {
    cd "$pkgbase-$_commit"
    export GOPATH="$srcdir"
    go mod download -modcacherw
}
pkgver() {
    date -u${SOURCE_DATE_EPOCH:+d @$SOURCE_DATE_EPOCH} +%Y%m%d%H%M%S
}
build() {
    cd "$pkgbase-$_commit"
    export CGO_CPPFLAGS="$CPPFLAGS"
    export CGO_CFLAGS="$CFLAGS"
    export CGO_CXXFLAGS="$CXXFLAGS"
    export CGO_LDFLAGS="$LDFLAGS"
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
    export GOPATH="$srcdir"
    NO_SKIP=true go run -v .
}
package_sing-geoip-db() {
    pkgdesc="Geoip database for sing-box."
    provides=(sing-geoip)

    install -Dvm644 -t "$pkgdir/usr/share/sing-box" \
        "$pkgbase-$_commit/geoip"*.db
}
package_sing-geoip-rule-set() {
    pkgdesc="Geoip rule sets for sing-box."
    provides=(sing-geoip)

    install -Dvm644 -t "$pkgdir/usr/share/sing-box" \
        "$pkgbase-$_commit/rule-set/geoip-"*.srs
}
