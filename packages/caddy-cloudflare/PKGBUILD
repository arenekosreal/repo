pkgname=caddy-cloudflare
pkgver=2.10.2
pkgrel=1
pkgdesc="Fast and extensible multi-platform HTTP/1-2-3 web server with automatic HTTPS."
url=https://caddyserver.com/
arch=(x86_64)
license=(Apache-2.0)
depends=(glibc)
makedepends=(go)
provides=("caddy=$pkgver")
conflicts=(caddy)
backup=(etc/caddy/Caddyfile)
source=("caddy-$pkgver.tar.gz::https://github.com/caddyserver/caddy/archive/refs/tags/v$pkgver.tar.gz"
		"https://raw.githubusercontent.com/caddyserver/dist/v$pkgver/init/caddy.service"
		"https://raw.githubusercontent.com/caddyserver/dist/v$pkgver/init/caddy-api.service"
		"https://raw.githubusercontent.com/caddyserver/dist/v$pkgver/config/Caddyfile"
		"https://raw.githubusercontent.com/caddyserver/dist/v$pkgver/welcome/index.html"
		use-data-dir-for-autosave.patch)
sha256sums=('f63f46b7ae68ced0a5c2e31df1b6dfc7656117d162a1bc7fed4bd4afd14ddc8f'
            '6c271e030644bd36a0c8956885934f16c928f88202bc126f12cde519ef9693ff'
            'a794bbf7d890eb9e1231bbad251890f87870815a96e3820b28a71819ba9f9c14'
            '66177d46fa761acb07208065db9b0274cb1b12c02ac43b9bfc9857b698b1ccfe'
            '70a45d667679109cd6b25502554597f21536ee995ada0549251167bd171533b9'
            '63c2e3f5b04deb28527fa0b5aeaef026f3580f4bcfb1ac66fe6213d007b42a39')

prepare() {
    cd "$srcdir/caddy-$pkgver"
    patch -N -i "$srcdir/use-data-dir-for-autosave.patch"
    local -ra _modules=(github.com/caddy-dns/cloudflare
                        github.com/WeidiDeng/caddy-cloudflare-ip
                        github.com/daegalus/caddy-anubis)
    local m
    for m in "${_modules[@]}"
    do
        sed -i "/plug in Caddy modules here/a _ \"$m\"" cmd/caddy/main.go
    done
    go get -modcacherw "${_modules[@]}" ./cmd/caddy
}

build() {
    cd "$srcdir/caddy-$pkgver"
    export CGO_LDFLAGS="${LDFLAGS}"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
    go build -ldflags "-X github.com/caddyserver/caddy/v2.CustomVersion=v$pkgver" ./cmd/caddy
    local i
    for i in zsh bash fish
    do
        ./caddy completion "$i" >"caddy.$i"
    done
}

package() {
    cd "$srcdir/caddy-$pkgver"
    install -Dm755 caddy "$pkgdir/usr/bin/caddy"
    install -Dm644 -t "$pkgdir/usr/lib/systemd/system" "$srcdir/caddy.service" "$srcdir/caddy-api.service"
    echo "u caddy - \"caddy daemon\" /var/lib/caddy" | install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/caddy.conf"
    install -Dm644 "$srcdir/Caddyfile" "$pkgdir/etc/caddy/Caddyfile"
    sed -i "s|/usr/share/caddy|/srv/http|g" "$pkgdir/etc/caddy/Caddyfile"
    install -Dm644 "$srcdir/index.html" "$pkgdir/usr/share/caddy/index.html"
    install -Dm644 caddy.zsh "$pkgdir/usr/share/zsh/site-functions/_caddy"
    install -Dm644 caddy.bash "$pkgdir/usr/share/bash-completion/completions/caddy"
    install -Dm644 caddy.fish "$pkgdir/usr/share/fish/vendor_completions.d/caddy.fish"
}
