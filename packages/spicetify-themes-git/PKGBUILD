# Contributor: strat <strat@jamjar.ws>

pkgname=spicetify-themes-git
pkgver=r509.3effbbe
pkgrel=1
pkgdesc="A community-driven collection of themes for spicetify."
arch=(any)
url=https://github.com/spicetify/spicetify-themes
license=(MIT)
makedepends=(git)
provides=(spicetify-themes)
conflicts=(spicetify-themes)
source=("git+$url")
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/spicetify-themes"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    depends+=(spicetify-cli)
    mkdir -p "$pkgdir/opt/spicetify-cli"
    cp -r --no-preserve=ownership "$srcdir/spicetify-themes" "$pkgdir/opt/spicetify-cli/Themes"

    echo "Removing useless files in each theme..."
    find "$pkgdir/opt/spicetify-cli/Themes" -mindepth 2 ! -name "color.ini" ! -name "user.css" ! -name "theme.js" -exec rm -rfv {} +
    echo "Removing useless files at top level..."
    find "$pkgdir/opt/spicetify-cli/Themes" -maxdepth 1 \( -name ".*" -o -name "_*" -o -type f \) -exec rm -rfv {} +
}
