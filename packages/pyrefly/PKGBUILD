# Maintainer: Antoine Lubineau <antoine@lubignon.info>
pkgname=pyrefly
pkgver=0.43.0
pkgrel=1
pkgdesc="A fast type checker and IDE for Python"
arch=("x86_64")
url="https://github.com/facebook/pyrefly"
license=("MIT")
makedepends=("cargo-nightly" "git")
options=(!lto)
source=("git+https://github.com/facebook/pyrefly#tag=${pkgver}")
b2sums=('20e5f06a1b9efff12491ba23be80c2b22179e0ee194fcf3d611af35a5084d92be6b2c6840791c11d3f45052302c4dbbe9ac92899d112dd93ed3b903aef134161')

prepare() {
  cd "${srcdir}/${pkgname}/${pkgname}"
  export RUSTUP_TOOLCHAIN=nightly
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${srcdir}/${pkgname}/${pkgname}"
  export RUSTUP_TOOLCHAIN=nightly
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

package() {
  install -D -m 0755 -t "${pkgdir}/usr/bin/" "${srcdir}/${pkgname}/${pkgname}/target/release/pyrefly"
  install -D -m 0644 -t "${pkgdir}/usr/share/licenses/${pkgname}/" "${srcdir}/${pkgname}/LICENSE"
}
