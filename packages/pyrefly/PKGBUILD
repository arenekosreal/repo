# Maintainer: Antoine Lubineau <antoine@lubignon.info>
pkgname=pyrefly
pkgver=0.41.0
pkgrel=1
pkgdesc="A fast type checker and IDE for Python"
arch=("x86_64")
url="https://github.com/facebook/pyrefly"
license=("MIT")
makedepends=("cargo-nightly" "git")
options=(!lto)
source=("git+https://github.com/facebook/pyrefly#tag=${pkgver}")
b2sums=('e974255cd4775e32eae77af219bbba84d1e6a14a3335ab91feac579c8ce2a66b6ec89c999bda4da40739d2eab759c1142ec59039a4ab6a5b7bbfc72168c423f1')

prepare() {
  cd "${srcdir}/${pkgname}/${pkgname}"
  export RUSTUP_TOOLCHAIN=nightly
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${srcdir}/${pkgname}/${pkgname}"
  export RUSTUP_TOOLCHAIN=nightly
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

package() {
  install -D -m 0755 -t "${pkgdir}/usr/bin/" "${srcdir}/${pkgname}/${pkgname}/target/release/pyrefly"
  install -D -m 0644 -t "${pkgdir}/usr/share/licenses/${pkgname}/" "${srcdir}/${pkgname}/LICENSE"
}
