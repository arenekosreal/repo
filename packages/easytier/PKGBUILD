_pkgname=EasyTier
pkgname=easytier
pkgver=2.4.3
pkgrel=1
pkgdesc="A simple, decentralized mesh VPN with WireGuard support."
arch=(x86_64)
url=https://easytier.cn/
license=(LGPL-3.0-only)
depends=(gcc-libs glibc)
makedepends=(clang rust pnpm nodejs-lts-jod protobuf)
source=("$_pkgname-$pkgver.tar.gz::https://github.com/EasyTier/EasyTier/archive/refs/tags/v$pkgver.tar.gz"
        "easytier-web.service"
        # Extracted from install script
        "easytier@.service"
        "default.conf")
sha256sums=('7079522fde96ea4c1089aa89eb24bb6829f4c74dbce0ee8d3b6825cd6df3baba'
            'c44aa47853b8939983e0545cd9d441736c65ec2aeab919b8b4e8d699bd6bd0ea'
            'ec7374291753688e1c5d5f183abcda09ce8d9edd4eca5ae8f0956f976f20451f'
            '23fe08cdafad2c4f384aa6080d3ed52341510760822b1dbbdbf2259cc112e742')
options=(!lto)
backup=(etc/easytier/default.conf)

prepare() {
    cd "$_pkgname-$pkgver"
    pnpm -r install
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "$_pkgname-$pkgver"
    pnpm -r --filter "./easytier-web/*" build
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    #RUSTFLAGS+=" --cfg tokio_unstable"
    #cargo build --frozen --release --all-features
    cargo build --frozen --release \
        --features easytier-web/embed \
        --features easytier/full \
        --features easytier/quic \
        --features easytier/jemalloc
}

package() {
    cd "$_pkgname-$pkgver"
    find target/release \
        -maxdepth 1 \
        -executable \
        -type f \
        -exec install -Dm0755 -t "$pkgdir/usr/bin/" {} +
    install -Dm0644 -t "$pkgdir/usr/lib/systemd/system/" \
        "$srcdir/easytier-web.service" \
        "$srcdir/easytier@.service" 
    install -Dm0644 "$srcdir/default.conf" "$pkgdir/etc/easytier/default.conf"
}
