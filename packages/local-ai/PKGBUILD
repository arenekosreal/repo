# Contributor: robertfoster

pkgbase=local-ai
pkgname=(local-ai
         local-ai-backend-llama-cpp-openblas
         local-ai-backend-huggingface
         local-ai-backend-local-store
         local-ai-backend-piper
         local-ai-backend-silero-vad
         local-ai-backend-stablediffusion-ggml
         local-ai-backend-whisper-openblas)
pkgver=3.11.0
pkgrel=1
pkgdesc="The free, Open Source alternative to OpenAI, Claude and others."
arch=(x86_64)
url=https://localai.io/
license=(MIT)
depends=(gcc-libs glibc bash)
makedepends=(cmake git go go.rice abseil-cpp grpc protobuf protoc-gen-go protoc-gen-go-grpc
             blas-openblas blas64-openblas onnxruntime)
source=("git+https://github.com/mudler/LocalAI.git#tag=v$pkgver"
        "local-ai.service"
        "0001-disable-installing-extra-tools.diff"
        "0004-avoid-gopiper-flags-being-applied-to-cmake.diff"
        "0005-do-not-clean-build-dir-in-whisper.diff"
        "0006-silero-vad-use-system-onnxruntime.diff")
sha256sums=('0172861c49681d3df5eae1e3847ae2fd162d32ee0ba1bfb245c5667983856792'
            '2cbecd8267b3807e88feffb2239a5cdfd3912fe1e78aa856a781cb39154536d0'
            '6ddb812ff27e3a402959eeed921f9ee1ef8ef4a5e9ebc04367e5fb9520e56fb1'
            'e308131cb6335a0240e26a9cb074f608641197d0435cabe7b5389f380596e8a3'
            '51e22e43047892579f67470a6b6bee8bf687d4a31b1a07e03671b7ca6c8821cc'
            '60f6a363f14188a22450c89e730a058e52185f7609e12e2a4a015ad4ad596c87')
# static resources are embedded into binary and cannot be stripped.
options=(!strip)

_llama_cpp_variants=(avx avx2 avx512 fallback grpc rpc-server)
_backends=(piper local-store huggingface silero-vad stablediffusion-ggml
           whisper)

_strip_file() {
    case "$(file -Sib "$1")" in
        application/x-sharedlib\;*)        # Libraries (.so)
            strip -v $STRIP_SHARED "$1" ;;
        application/x-archive\;*)          # Libraries (.a)
            strip -v $STRIP_STATIC "$1" ;;
        application/x-executable\;*)       # Binaries
            strip -v $STRIP_BINARIES "$1" ;;
        application/x-pie-executable\;*)   # Relocatable binaries
            strip -v $STRIP_SHARED "$1" ;;
    esac
}

prepare() {
    cd "$srcdir/LocalAI"
    patch -Np1 -i ../0001-disable-installing-extra-tools.diff
    patch -Np1 -i ../0005-do-not-clean-build-dir-in-whisper.diff
    patch -Np1 -i ../0006-silero-vad-use-system-onnxruntime.diff
    make -C backend/cpp/llama-cpp llama.cpp
    make -C backend/go/piper sources/go-piper
    make -C backend/go/stablediffusion-ggml sources/stablediffusion-ggml.cpp
    make -C backend/go/whisper sources/whisper.cpp
    patch -Np1 -d backend/go/piper/sources/go-piper -i "$srcdir/0004-avoid-gopiper-flags-being-applied-to-cmake.diff"
}

build() {
    cd "$srcdir/LocalAI"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
    # -ldfalgs=-linkmode=external is not supported when CGO_ENABLED=0
    echo "Building main program..."
    make GO_TAGS=stablediffusion,tts,p2p \
         build
    echo "Building llama-cpp..."
    local variant
    for variant in "${_llama_cpp_variants[@]}"
    do
        make -C backend/cpp/llama-cpp \
             BUILD_TYPE=openblas \
             "llama-cpp-$variant"
    done
    local backend
    # Golang backends
    for backend in "${_backends[@]}"
    do
        echo "Building $backend..."
        make -C "backend/go/$backend" \
            BUILD_TYPE=openblas \
            "$backend"
    done
}

package_local-ai() {
    install=$pkgname.install
    optdepends=("local-ai-backend: Packaged backends")

    cd "$srcdir/LocalAI"
    mkdir -p "$pkgdir/usr/bin"
    install -Dvm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
    install -Dvm644 "$srcdir/$pkgbase.service" "$pkgdir/usr/lib/systemd/system/$pkgbase.service"
    install -Dvm644 ".env" "$pkgdir/usr/share/doc/$pkgbase/$pkgbase.env"
    install -Dvm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgbase/LICENSE"
}

package_local-ai-backend-llama-cpp-openblas() {
    pkgdesc+=" (llama-cpp backend with openblas optimization)"
    groups=(local-ai-backend)
    provides=(local-ai-backend-llama-cpp)
    conflicts=(local-ai-backend-llama-cpp)
    depends+=(abseil-cpp grpc openblas protobuf)

    cd "$srcdir/LocalAI"
    local variant
    for variant in "${_llama_cpp_variants[@]}"
    do
        install -Dvm755 "backend/cpp/llama-cpp/llama-cpp-$variant" \
            "$pkgdir/usr/lib/$pkgbase/backends/llama-cpp/llama-cpp-$variant"
        _strip_file "$pkgdir/usr/lib/$pkgbase/backends/llama-cpp/llama-cpp-$variant"
    done
    install -Dvm755 "backend/cpp/llama-cpp/run.sh" "$pkgdir/usr/lib/$pkgbase/backends/llama-cpp/run.sh"
    install -Dvm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_local-ai-backend-huggingface() {
    pkgdesc+=" (huggingface backend)"
    groups=(local-ai-backend)

    cd "$srcdir/LocalAI"
    install -Dvm755 -t "$pkgdir/usr/lib/$pkgbase/backends/huggingface/" \
        "backend/go/huggingface/huggingface" \
        "backend/go/huggingface/run.sh"
    _strip_file "$pkgdir/usr/lib/$pkgbase/backends/huggingface/huggingface"
    install -Dvm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE" 
}

package_local-ai-backend-local-store() {
    pkgdesc+=" (local-store backend)"
    groups=(local-ai-backend)

    cd "$srcdir/LocalAI"
    install -Dvm755 -t "$pkgdir/usr/lib/$pkgbase/backends/local-store/" \
        "backend/go/local-store/local-store" \
        "backend/go/local-store/run.sh"
    _strip_file "$pkgdir/usr/lib/$pkgbase/backends/local-store/local-store"
    install -Dvm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_local-ai-backend-piper() {
    pkgdesc+=" (piper backend)"
    groups=(local-ai-backend)

    cd "$srcdir/LocalAI"
    install -Dvm755 -t "$pkgdir/usr/lib/$pkgbase/backends/piper/" \
        "backend/go/piper/piper" \
        "backend/go/piper/run.sh"
    local pi_lib="backend/go/piper/sources/go-piper/piper-phonemize/pi/lib"
    find "$pi_lib" -mindepth 1 -maxdepth 1 -type f -exec \
        install -Dvm644 -t "$pkgdir/usr/lib/$pkgbase/backends/piper/lib/" {} +
    find "$pi_lib" -mindepth 1 -maxdepth 1 -type l -exec \
        cp -Pv -t "$pkgdir/usr/lib/$pkgbase/backends/piper/lib/" {} +
    local f
    while read -r f
    do
        _strip_file "$f"
    done < <(find "$pkgdir/usr/lib/$pkgbase/backends/piper" -type f \( -executable -o -name "*.so" \) -a ! -name "*.sh")
    cp -avf "backend/go/piper/espeak-ng-data" "$pkgdir/usr/lib/$pkgbase/backends/piper/" 
    install -Dvm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_local-ai-backend-silero-vad() {
    pkgdesc+=" (silero-vad backend)"
    groups=(local-ai-backend)
    depends+=(onnxruntime)

    cd "$srcdir/LocalAI"
    install -Dvm755 -t "$pkgdir/usr/lib/$pkgbase/backends/silero-vad/" \
        "backend/go/silero-vad/silero-vad" \
        "backend/go/silero-vad/run.sh"
    local f
    while read -r f
    do
        _strip_file "$f"
    done < <(find "$pkgdir/usr/lib/$pkgbase/backends/silero-vad" -type f \( -executable -o -name "*.so" \) -a ! -name "*.sh")
    install -Dvm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_local-ai-backend-stablediffusion-ggml() {
    pkgdesc+=" (stablediffusion-ggml backend)"
    groups=(local-ai-backend)

    cd "$srcdir/LocalAI"
    install -Dvm755 -t "$pkgdir/usr/lib/$pkgbase/backends/stablediffusion-ggml/" \
        "backend/go/stablediffusion-ggml/stablediffusion-ggml" \
        "backend/go/stablediffusion-ggml/run.sh"
    _strip_file "$pkgdir/usr/lib/$pkgbase/backends/stablediffusion-ggml/stablediffusion-ggml"
    install -Dvm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_local-ai-backend-whisper-openblas() {
    pkgdesc+=" (whisper backend with openblas optimization)"
    groups=(local-ai-backend)
    provides=(local-ai-backend-whisper)
    conflicts=(local-ai-backend-whisper)

    cd "$srcdir/LocalAI"
    install -Dvm755 -t "$pkgdir/usr/lib/$pkgbase/backends/whisper/" \
        "backend/go/whisper/whisper" \
        "backend/go/whisper/run.sh"
    _strip_file "$pkgdir/usr/lib/$pkgbase/backends/whisper/whisper"
    install -Dvm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
