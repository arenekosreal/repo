# Contributor: robertfoster

pkgname=local-ai
pkgver=3.1.1
pkgrel=1
pkgdesc="The free, Open Source alternative to OpenAI, Claude and others."
arch=(x86_64)
url=https://localai.io/
license=(MIT)
depends=(gcc-libs glibc grpc protobuf abseil-cpp zlib openblas)
makedepends=(cmake git go go.rice protoc-gen-go protoc-gen-go-grpc upx blas-openblas blas64-openblas)
source=("git+https://github.com/mudler/LocalAI.git#tag=v$pkgver"
        "local-ai.service")
sha256sums=('4cea676a1e284fdd338bc7df3b4346b317647e678f856a36137747e915cee0ed'
            'c62647f59de40858cafe68da0495ca1e4158160d006ca56b4bbd0efa34d41737')
options=(!strip)

prepare() {
    cd "$srcdir/LocalAI"
    make prepare-sources
    sed -i "s/ -lfmt -lspdlog -lucd//;s/-I./-I\$(INCLUDE_PATH)/g;s/-L./-L\$(LIBRARY_PATH)/g" sources/go-piper/Makefile
    sed -i "s/3.3/3.5/" sources/bark.cpp/encodec.cpp/ggml/CMakeLists.txt
}

build() {
    cd "$srcdir/LocalAI"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
    make BUILD_TYPE=openblas \
         GO_TAGS=stablediffusion,tts,p2p \
         CGO_LDFLAGS_WHISPER="${CGO_LDFLAGS}" \
         build
}

package() {
    cd "$srcdir/LocalAI"
    install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
    install -Dm644 "$srcdir/$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
    install -Dm644 .env "$pkgdir/usr/share/doc/$pkgname/$pkgname.env"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
