pkgname=neocmakelsp
pkgver=0.8.25
pkgrel=1
pkgdesc="CMake LSP implementation based on Tower and Tree-sitter."
arch=(x86_64)
url=https://neocmakelsp.github.io/
license=(MIT)
depends=(cmake)
makedepends=(meson rust)
options=(!lto)
source=("$pkgname-$pkgver.tar.gz::https://github.com/neocmakelsp/neocmakelsp/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('2e4e1b61ea4d426bbfa615cffb847ed866ee8b8477f114c648567fc7750c482e')


prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    arch-meson "$pkgname-$pkgver" build
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
    install -Dm644 "$srcdir/$pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
